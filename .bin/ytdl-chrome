#! /usr/bin/env fish
argparse -i 'a/all' 'e/edit' 'f/file=' -- $argv
set -l urls
if test -n "$_flag_a"
  echo "Downloading ALL tabs from front window"
  set urls (osascript -l JavaScript -e "Application('Google Chrome').windows[0].tabs().map(t => t.url()).join('\n')")
else
  set urls (osascript -l JavaScript -e "Application('Google Chrome').windows[0].activeTab.url()")
end

if test -n "$_flag_e"
  set urls (string join \n $urls | vipe)
end

if test -z "$urls"
  echo "Nothing to do"
  exit 0
end

if test -n "$_flag_f"
  string join \n $urls >> $_flag_f
else
  youtube-dl -i -a (string join \n $urls | psub) $argv
end
