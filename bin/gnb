#!/usr/bin/env fish

function __gnb_new_branch
  read -d \t -a -l args
  set -l ticket $args[1]
  set -l name (string trim $args[2] | string lower | string replace -a ' ' "-" -- | string replace -a -r "[^\-0-9a-z]" "" --)
  set -l branchname "jeffs/$ticket/$name"
  git switch -c $branchname
  git config "branch.$branchname.description" "Branch for JIRA ticket $ticket

  Ticket-Ref: $ticket
  Ticket-Name: $args[2]"
end

jira list --template list-separated --query "resolution = unresolved and assignee=currentuser() ORDER BY priority asc, created" | \
  fzf \
    --delimiter "\t" \
    --reverse \
    --preview="jira view {1}" \
    --bind="ctrl-e:execute(jira view -b {1})" | \
  __gnb_new_branch
