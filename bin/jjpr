#!/usr/bin/env fish
argparse -i "c/change=+" "p/push-args=" "r/ref=" -- $argv

set -l push_args
if set -q _flag_ref
  set -a push_args -N -r $_flag_ref
else
  set -q _flag_push_args; or set -q _flag_change; or set -a push_args "-c" "@-"
  for rev in $_flag_change
    set -a push_args -c $rev
  end
end

set -a push_args $_flag_push_args

jj git push $push_args &| read -z push_result

set -S push_result

string match -q -g --all --regex  "Add bookmark (?<bookmarks>.*)\*? to" $push_result

set -S bookmarks

if not set -q bookmarks[1]
  set bookmarks (jj log -r "heads(::@ & bookmarks(glob:'jeffs/push/*'))" -T "bookmarks ++ '"\n"'" --no-graph | string replace '*' '' )
end


for bookmark in $bookmarks
  echo (set_color --bold cyan)"Opening PR for $bookmark"(set_color reset)
  jj log -r "trunk()-..$bookmark"
  gh pr create -H $bookmark
end
